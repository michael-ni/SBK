/**
 * Copyright (c) KMG. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 */

import org.gradle.internal.jvm.Jvm

buildscript {
	println "Build JVM Version is : " + Jvm.current()

	repositories {
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		//classpath "gradle.plugin.com.palantir.graal:gradle-graal:0.6.0-122-g9fba612"
		classpath "gradle.plugin.org.mikeneck:graalvm-native-image-plugin:0.5.0"
	}

}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
//apply plugin: "com.palantir.graal"
apply plugin: "org.mikeneck.graalvm-native-image"

def SbkMainClass = "io.sbk.main.SbkMain"

allprojects {
	repositories {
		mavenLocal()
		jcenter()
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}

	apply plugin: 'java'
	apply plugin: 'application'

	dependencies {
		compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: spotbugsAnnotationsVersion
		testCompile group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: spotbugsAnnotationsVersion
	}

	apply from: "$rootDir/gradle/application.gradle"
	apply from: "$rootDir/gradle/checkstyle.gradle"
	apply from: "$rootDir/gradle/eclipse.gradle"
	apply from: "$rootDir/gradle/spotbugs.gradle"
	apply from: "$rootDir/gradle/idea.gradle"
	apply from: "$rootDir/gradle/jacoco.gradle"
	apply from: "$rootDir/gradle/java.gradle"
	apply from: "$rootDir/gradle/maven.gradle"
	apply from: "$rootDir/gradle/protobuf.gradle"

	mainClassName = SbkMainClass

	jar {
		manifest {
			attributes	'Implementation-Title': rootProject.name,
						'Implementation-Version': sbkVersion
		}
	}

	startScripts {
		doLast {
			unixScript.text = unixScript.text.replace('SERVER_APP_HOME', '\$APP_HOME')
			windowsScript.text = windowsScript.text.replace('SERVER_APP_HOME', '%~dp0..')
		}
	}
}

/* Include your driver below */
dependencies {
	compile project(":sbk-api")
	compile project(":driver-concurrentq")
	compile project(":driver-file")
	compile project(":driver-asyncfile")
	compile project(":driver-hdfs")
	compile project(":driver-bookkeeper")
	compile project(":driver-rabbitmq")
	compile project(":driver-rocketmq")
	compile project(":driver-pulsar")
	compile project(":driver-kafka")
	compile project(":driver-pravega")
	compile project(":driver-nats")
	compile project(':driver-natsStream')
	compile project(':driver-artemis')
	compile project(':driver-nsq')
	compile project(':driver-jdbc')
}

//create a single Jar with all dependencies
task buildJar(type: Jar) {
	manifest {
		attributes	'Implementation-Title': rootProject.name,
					'Implementation-Version': sbkVersion,
					'Main-Class': SbkMainClass
	}
	zip64 true
	baseName = rootProject.name
	from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
	exclude 'META-INF/*.RSA'
	exclude 'META-INF/*.SF'
	exclude 'META-INF/*.DSA'
	with jar
}


nativeImage {
	graalVmHome = "$System.env.GRAALVM_HOME"
	mainClass = SbkMainClass
	executableName = 'sbkgral'
	arguments(
			'--no-fallback',
			//         '--static'
			//          '--enable-all-security-services',
			//          '--initialize-at-run-time=com.example.runtime',
			'--report-unsupported-elements-at-runtime'
	)
}
